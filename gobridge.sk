options:
	gobridgePos: 108.5, 82, 16
	gobridgeWorld: "god"
	rooms: 7
	distance: 4
	length: 20
	timeup: 40
	commandOnWin: /gobridgehub	# This command is executed by player
	commandOnWin2: /eco give	# Executed by console. Argument of the command will be player and the win amount
	maxReward: 50			# Biggest reward that a player can get with gobridge
	winBlock: purpur pillar
	gobridgeHub: 101, 72, -161
	gobridgeHubDirection: 90, 0

command /gobridgehub:
	executable by: players
	trigger:
		if {gobridge.p::%player%} is true:
			stop
		teleport player to location({@gobridgeHub}, {@gobridgeWorld}, {@gobridgeHubDirection})

command /gobridge:
	executable by: players
	trigger:
		if {gobridge.p::%player%} is true:
			send "§4You are already godbridging"
			stop
		loop {@rooms} times:
			#set block 1 below ({@distance} * loop-number) east location({@gobridgePos}, {@gobridgeWorld}) to grass block

			if {gobridge.r::%loop-number%} is not set:
				set {gobridge.r::%loop-number%} to true
				set {gobridge.ur::%player%} to loop-number


				set {gobridge.p::%player%} to true
				teleport player to position ({@distance} * loop-number - {@distance}) east location({@gobridgePos}, {@gobridgeWorld}, 180, 70) 
				clear player's inventory
				give ({@length} * 2) red stained glass to player
				set {gobridge.t2::%player%} to Unix Timestamp of now

				stop

				
		send "§4Sorry. All the gobridge rooms are currently full."


every 1 second:
	loop all players:
		{gobridge.p::%loop-player%} is true
		if loop-player's y position + 2 < location({@gobridgePos})'s y position:
			clear {gobridge.p::%loop-player%}
			clear {gobridge.t::%loop-player%}
			clear {gobridge.t2::%loop-player%}
			clear {gobridge.r::%{gobridge.ur::%loop-player%}%}
			clear {gobridge.ur::%loop-player%}

			send "§cGame Over" to loop-player
			clear loop-player's inventory
			make loop-player execute command {@commandOnWin}
		else if unix timestamp of now - {gobridge.t2::%loop-player%} > {@timeup}:
			clear {gobridge.p::%loop-player%}
			clear {gobridge.t::%loop-player%}
			clear {gobridge.t2::%loop-player%}
			clear {gobridge.r::%{gobridge.ur::%loop-player%}%}
			clear {gobridge.ur::%loop-player%}


			send "§cTime Up!" to loop-player
			clear loop-player's inventory
			make loop-player execute command {@commandOnWin}


on block break:
	if {gobridge.p::%player%} is true:
		cancel event

on block place:
	if {gobridge.p::%player%} is true:
		if event-blocks' y position is not (block at location({@gobridgePos}))'s y position - 1:
			cancel event
		wait 3 seconds
		break event-block

on step on {@winBlock}:
	if {gobridge.p::%player%} is true:
		set {_t} to unix timestamp of now - {gobridge.t::%player%}
		clear {gobridge.p::%player%}
		clear {gobridge.t::%player%}
		clear {gobridge.t2::%player%}
		clear player's inventory

		set {_mt} to {@length} * 0.95
		set {_amt} to max(round((({_mt} - {_t}) / ({_mt} * 0.8) ) * {@maxReward}), 0)
		
		send "§aYou completed the challenge in %{_t}% seconds%nl%§aReward: %{_amt}%$"


		wait 1 second

		clear {gobridge.r::%{gobridge.ur::%player%}%}
		clear {gobridge.ur::%player%}


		make player execute command "{@commandOnWin}"
		execute console command "{@commandOnWin2} %player% %{_amt}%"


on player move:
	{gobridge.p::%player%} is true
	{gobridge.t::%player%} is not set
	set {gobridge.t::%player%} to unix timestamp of now



on script load:
	clear {gobridge.r::*}

command /gbdebug:
	permission: gbdebug
	trigger:
		send "a %{gobridge.r::*}%"
		clear {gobridge.r::*}

